10 PRINT TAB(25);"CONCENTRATION"
20 PRINT TAB(15);"CREATIVE COMPUTING  MORRISTOWN NEW JERSEY"
30 PRINT
40 PRINT
50 PRINT
200 DIM C$(52)
210 FOR X=1 TO 52
220 READ E$
230 C$(X)=E$
240 NEXT X
250 REM --   SHUFFLE AND DEAL
260 FOR Z=1 TO 51
270 K$=C$(Z)
275 L=INT((53-Z)*RND(1)+1)
280 C$(Z)=C$(L+Z-1)
290 C$(L)=K$
300 NEXT Z
340 REM --   START TO PLAY
350 FOR N=1 TO 26
360 PRINT "FIRST CARD";
370 INPUT U
372 IF U > 0 AND U < 52 THEN 380
374 PRINT "THERE ARE ONLY 52 CARDS IN THE DECK, NOT ";U
376 GOTO 360
380 LET G=1
390 IF C$(U)=" " THEN 840
400 PRINT "SECOND CARD";
410 INPUT W
412 IF W > 0 AND W < 52 THEN 420
414 PRINT "THERE ARE ONLY 52 CARDS IN THE DECK, NOT ";W
416 GOTO 400
420 LET G=2
430 IF C$(W)=" " THEN 840
440 IF U <> W THEN 470
450 PRINT "YOU CAN'T PICK THE SAME CARD TWICE!"
460 GOTO 400
470 IF MID$(C$(U),1,1)=MID$(C$(W),1,1) THEN 580
480 PRINT "#";U;"IS ";C$(U),"#";W;"IS ";C$(W),
490 FOR I=1 TO 50
500 PRINT;
510 NEXT I
520 PRINT CHR$(13);"HHHHHHHHHHHHHHHHHHHHHHHHHHHHHH";
530 PRINT CHR$(13);"IIIIIIIIIIIIIIIIIIIIIIIIIIIIII";
540 PRINT CHR$(13);"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"
550 PRINT
560 PRINT
570 GOTO 630
580 PRINT "THAT'S A MATCH --";C$(U),C$(W)
590 LET C$(U)=" "
600 LET C$(W)=" "
610 LET S=S+1
620 PRINT "YOUR SCORE IS NOW";S;" YOU HAVE HAD ";N;"PICKS."
630 NEXT N
640 REM --   THE RESULTS
650 LET S1=S/(N/4)
660 PRINT "YOU SCORED";S;" OUT OF ";N;"THAT IS ";
670 ON S1+1.5 GOTO 680,700,720,760
680 PRINT "POOR."
690 GOTO 770
700 PRINT "FAIR."
710 GOTO 770
720 PRINT "GOOD."
730 GOTO 770
740 PRINT "EXCELLENT ! ! !"
750 GOTO 770
760 PRINT ". . . AAAH . . . UH....YOU MUST HAVE CHEATED!"
770 PRINT
780 PRINT "DO YOU WANT TO PLAY AGAIN";
790 INPUT Z$
800 IF Z$ = "YES" THEN 260
810 PRINT
820 PRINT "COME BACK AGAIN!!"
830 END
840 PRINT "YOU HAVE ALREADY MATCHED THAT CARD."
850 IF G=1 THEN 360
860 GOTO 400
870 DATA "AS","2S","3S","4S","5S","6S","7S","8S","9S","10S","JS","QS"
875 DATA "KS"
880 DATA "AH","2H","3H","4H","5H","6H","7H","8H","9H","10H","JH","QH"
885 DATA "KH"
890 DATA "AD","2D","3D","4D","5D","6D","7D","8D","9D","10D","JD","QD"
895 DATA "KD"
900 DATA "AC","2C","3C","4C","5C","6C","7C","8C","9C","10C","JC","QC"
905 DATA "KC"
910 END
