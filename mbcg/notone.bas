3 PRINT TAB(26);"NOTONE":PRINT
5 PRINT TAB(20);"CREATIVE COMPUTING"
6 PRINT TAB(19);"MORRISTOWN NEW JERSEY"
7 PRINT:PRINT:PRINT
10 DIM T(50),R(10),C(10),L(12)
15 INPUT "WOULD YOU LIKE THE INSTRUCTIONS";A$
20 IF A$="YES" THEN 35
25 IF A$="NO" THEN 100
30 PRINT:PRINT "ANSWER YES OR NO!!":PRINT:GOTO 15
35 PRINT:PRINT "THE GAME OF NOTONE IS PLAYED WITH"
40 PRINT "TWO PLAYERS AND A PAIR OF DICE. THERE ARE"
45 PRINT "TEN ROUNDS IN THE GAME ONE ROUND CONSISTING"
50 PRINT "OF ONE TURN FOR EACH PLAYER. PLAYERS"
55 PRINT "(YOURSELF AND THE COMPUTER) ADD THE SCORE"
60 PRINT "THEY ATTAIN ON EACH ROUND, AND THE PLAYER"
62 PRINT "WITH THE HIGHEST SCORE AFTER TEN ROUNDS IS THE WINNER":PRINT
67 PRINT "ON EACH TURN THE PLAYER MAY ROLL THE TWO"
69 PRINT "DICE FROM 1 TO N TIMES. IF T1 IS THE TOTAL OF DICE ON"
72 PRINT "THE ITH ROLL, THEN THE PLAYERS SCORE FOR THE TURN IS"
75 PRINT "T(1)+T(2)+T(3)+........+T(N).  HOWEVER,"
77 PRINT "AND HERE'S THE CATCH, IF ANY T(I) IS EQUAL TO T(1) THEN"
80 PRINT "THE TURN IS OVER AND HIS SCORE FOR THAT ROUND IS ZERO"
82 PRINT "AFTER EACH ROLL THAT DOESN'T EQUAL T(1), THE PLAYER CAN"
88 PRINT "DECIDE WHETHER TO ROLL AGAIN OR STOP AND "
90 PRINT "SCORE THE NUMBER OF POINTS ALREADY OBTAINED."
100 FOR T=1 TO 10:PRINT:PRINT "ROUND ";T
110 X=X+1:R1=INT(6*RND(1))+1
115 R2=INT(6*RND(1))+1:PRINT R1+R2
120 IF X>1 THEN 130
125 T(1)=R1+R2:GOTO 135
130 T(X)=R1+R2
131 IF T(1)<>T(X) THEN  135
132 PRINT "YOU GET A ZERO FOR THIS ROUND"
133 X=0:T1=0:GOTO 200
135 T1=T1+T(X)
140 INPUT "ROLL AGAIN ";B$
145 IF B$="YES" THEN 110
150 IF B$="NO" THEN R(T)=T1:X=0:T1=0:FOR A=1 TO 50:T(A)=0:NEXT:GOTO 200
160 PRINT:PRINT "ANSWER YES OR NO!!":PRINT: GOTO 140
200 PRINT:PRINT TAB(15);"COMPUTERS MOVE":PRINT
201 RESTORE
202 R1=INT(6*RND(1))+1:R2=INT(6*RND(1))+1
204 FOR D=2 TO R1+R2:READ L(D):NEXT:D=R1+R2
205 FOR C=1 TO L(D):IF C=1 THEN 215
210 R1=INT(6*RND(1))+1:R2=INT(6*RND(1))+1
215 PRINT "COMPUTER'S ROLL"C":";R1+R2
220 IF C>1 THEN 230
225 T(1)=R1+R2:GOTO 242
230 T(C)=R1+R2
235 IF T(C)<>T(1) THEN 242
236 PRINT "THE COMPUTER GETS A ZERO FOR THE TURN!!"
237 T1=0:GOTO 245
242 T1=T1+T(C):NEXT C
245 C(T)=T1:T1=0:X=0
250 C2=C2+C(T):C1=C1+R(T)
253 PRINT:FOR B=1 TO 50:T(B)=0:NEXT
255 IF T=10 THEN PRINT "FINAL SCORE":PRINT
260 IF C2<C1 THEN 270
263 PRINT"COMPUTER: ";C2,"YOU: ";C1:GOTO 300
270 PRINT "YOU:";C1;"COMPUTER:";C2
300 NEXT T
305 DATA 18,18,9,9,6,6,6,9,9,18,18
310 PRINT:PRINT "SCORING SUMMARY":PRINT
315 PRINT "ROUND";TAB(15);"YOU";TAB(25);"COMPUTER":PRINT
320 FOR E=1 TO 10:PRINT E;TAB(16);R(E);TAB(26);C(E):NEXT
325 PRINT:PRINT "TOTALS: ";TAB(16);C1;TAB(26);C2:PRINT
