10 PRINT TAB(21);"MOTORCYCLE JUMP"
20 PRINT TAB(20);"CREATIVE COMPUTING"
30 PRINT TAB(18);"MORRISTOWN, NEW JERSEY"
40 PRINT:PRINT:PRINT
50 DIM I2(14),I$(84)
90 I$="R.ARM L.ARM R.LEG L.LEG BACK  NECK  SKULL RIBS  KNEE  BUTT  "
100 I$=I$+"FACE  PELVISPRIDE BIKE  "
110 T=.1
130 T2=0
140 T3=0
150 PRINT "WE'RE AT THE SCENE OF THE BIG MOTORCYCLE JUMP!"
160 INPUT "HOW MANY BUSSES WILL YOU TRY TO JUMP";N
170 J=N*15
180 PRINT N;" BUSSES!  THAT'S ";J;" FEET!"
190 INPUT "WHAT RAMP ANGLE WILL YOU USE";A2
200 IF A2<90 AND A2>0 THEN 230
210 PRINT A2;" DEGREES? THAT'S IMPOSSIBLE.  COME ON NOW, "
220 GOTO 190
230 A=A2*.01745
240 INPUT "HOW FAST WILL YOU LEAVE THE RAMP";S
250 PRINT "      GOOD LUCK!"
260 IF S>0 THEN 300
270 PRINT "A PRACTICE JUMP!"
280 PRINT "OK, THIS TIME ";
290 GOTO 240
300 H=6
310 D=0
320 G=6
330 R2=0
340 S2=0
350 S=S*1.5
360 PRINT "THERE HE GOES!!!!"
380 S=S-S2
390 F=S*T
400 D2=F*COS(A)
410 R=F*SIN(A)
420 R2=R2+(32*T)
430 R3=R2*T
440 H=H+R-R3
450 D=D+D2
460 PRINT "*";
480 S2=(S/120)*32*T
490 IF D>=J THEN G=G-R
510 IF G<=0 THEN G=0
520 IF H>G THEN 380
530 IF D<J THEN 600
540 IF D>J+20 THEN 640
550 L=((D-J)/30)+RND(1)
560 IF L>.8 THEN 650
570 PRINT "HE MADE IT !  GREAT JUMP, KILLER!"
580 T2=T2+1
590 GOTO 920
600 PRINT "HE'S SHORT OF THE RAMP ....."
620 L2=INT((((J-D)/5)*2)+(RND(1)*5)+.5)
630 GOTO 670
640 PRINT "HE JUMPED TOO FAR!"
650 PRINT "HE MISSED THE RAMP."
660 L2=INT(((D+20-J)/20)+(RND(1)*5))
670 PRINT "I THINK HE'S HURT......"
680 FOR K=1 TO 14
690 I2(K)=K
700 NEXT K
710 K2=14
720 IF L2>14 THEN L2=14
730 IF L2<=0 THEN L2=1
760 FOR K=1 TO L2
770 V=INT(RND(1)*1000)
780 V=(V-(INT(V/K2)*K2))+1
790 H2=I2(V)
800 I2(V)=I2(K2)
810 I2(K2)=H2
820 K2=K2-1
830 NEXT K
840 PRINT "WELL, KILLER, THE DOCTOR SAYS YOU BROKE YOUR:"
860 FOR K=(15-L2) TO 14
880 P=(6*I2(K))-5
890 A$=MID$(I$,P,6)
900 PRINT A$
910 NEXT K
920 T3=T3+1
930 INPUT "WANT TO JUMP AGAIN";A$
940 IF LEFT$(A$,1)="Y" THEN PRINT:PRINT:GOTO 160
950 PRINT "YOU MADE IT ";T2;" OUT OF ";T3;" ATTEMPTS."
960 PRINT "BE CAREFUL, NOW."
970 END
