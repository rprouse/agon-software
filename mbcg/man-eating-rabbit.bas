1 PRINT TAB(20);"MAN-EATING RABBIT"
2 PRINT TAB(20);"CREATIVE COMPUTING"
3 PRINT TAB(18);"MORRISTOWN, NEW JERSEY"
4 PRINT:PRINT:PRINT
10 PRINT "YOU ARE IN A PIT WITH A MAN-EATING RABBIT."
15 PRINT "THE CENTER IS (0,0) AND IT HAS A RADIUS OF 10"
20 PRINT "IF YOU CAN AVOID THE RABBIT FOR 10 MOVES YOU WILL BE"
25 PRINT "RELEASED.  YOU AND THE RABBIT CAN MOVE ONLY 1 SPACE EACH"
26 PRINT "HOWEVER THE RABBIT CAN DO MULTIPLE JUMPS."
27 PRINT "YOU CAN TRAVEL AT THESE ANGLES"
28 PRINT "0,45,90,135,180,225,270,315,340"
40 X=INT(21*RND(1)-10)
41 Y=INT(21*RND(1)-10)
45 D=SQR(ABS((X-X1)^2+(Y-Y1)^2))
46 IF D>10 THEN 40
47 PRINT
48 PRINT
50 PRINT "WHERE WOULD YOU LIKE TO BE DROPPED";
51 INPUT X1,Y1
60 IF SQR(ABS(X1^2+Y1^2))>10 THEN 50
65 IF X<>X1 THEN 70
66 IF Y<>Y1 THEN 70
67 PRINT "*****SQUISH*****"
69 PRINT "THE RABBIT IS DEAD!  YOU ARE SET FREE!":GOTO 340
70 FOR G=1 TO 10
71 D=SQR(ABS((X-X1)^2+(Y-Y1)^2))
80 PRINT "RABBIT AT (";X;",";Y;") AND DISTANCE ";D
90 IF D=0 THEN 330
91 PRINT:PRINT "TURN #";G;" HUMAN AT (";X1;",";Y1;")"
100 PRINT "AT WHAT ANGLE WILL YOU RUN ";
101 INPUT A
110 IF A/45<>INT(A/45) THEN 100
111 PRINT "RUNNING ......";:P1=1
112 M=1:IF ABS((INT(A/10)*10)-A)<>5 THEN 120
113 M=SQR(2)
120 X2=(M*COS(A*(3.14159/180)))
121 Y2=(M*SIN(A*(3.14159/180)))
125 IF SQR(((X1+X2)^2+(Y1+Y2)^2))<=10 THEN 130
126 PRINT "YOU CAN'T GO INTO A WALL!"
127 GOTO 100
130 X1=INT(X1*1000)/1000+X2
131 Y1=INT(Y1*1000)/1000+Y2
132 X1=INT(X1+.5)
133 Y1=INT(Y1+.5)
135 PRINT "HUMAN YOU ARE NOW AT (";X1;",";Y1;")"
136 IF X<>X1 THEN 140
137 IF Y<>Y1 THEN 140
138 PRINT "YOU RAN RIGHT INTO THE RABBIT!!"
139 GOTO 330
140 PRINT "THE RABBIT IS POUNCING AT ANGLE.....";:P1=P1+1
150 X2=X1-X:Y2=Y1-Y
151 IF X2=0 THEN 280
152 IF Y2=0 THEN 300
160 B=INT(ATN(ABS((Y2/X2)))/(3.14159/180))
170 ON SGN(X2)+2 GOTO 190,10,180
180 ON SGN(Y2)+2 GOTO 240,10,250
190 ON SGN(Y2)+2 GOTO 230,10,220
220 B=180-B:GOTO 250
230 B=B+180:GOTO 250
240 B=360-B
250 B=INT(B/45+.5)*45:PRINT B
255 M=1
256 IF ABS((INT(B/10)*10)-B)<>5 THEN 260
257 M=1.5
260 X2=(M*COS(B*(3.14159/180)))
261 Y2=(M*SIN(B*(3.14159/180)))
270 X=INT(X+X2+.5)
271 Y=INT(Y+Y2+.5)
272 GOTO 315
280 IF Y2<0 THEN 290
281 B=90:GOTO 315
290 B=270:GOTO 250
300 IF X2<0 THEN 310
301 B=1:GOTO 250
310 B=180:GOTO 250
315 IF SQR((X-X1)^2+(Y-Y1)^2)=0 THEN 323
320 P=INT(P1*RND(1)+1)
321 IF P<>1 THEN 323
322 GOTO 140
323 NEXT G:PRINT "YOU ARE RELEASED!":GOTO 340
330 PRINT "**CRUNCH** WELL R.I.P."
340 END
