10 PRINT TAB(28);"TWO TO TEN"
20 PRINT TAB(15);"CREATIVE COMPUTING  MORRISTOWN NEW JERSEY"
30 PRINT
40 PRINT
50 PRINT
60 PRINT "WELCOME TO THE GAME OF TWO TO TEN.  THAT NAME COMES FROM THE"
70 PRINT "SPECIAL 'DECK OF CARDS' USED. THERE ARE NO FACE CARDS - ONLY"
80 PRINT "THE CARDS 2-10.  THIS GAME IS EASY AND FUN TO PLAY IF YOU"
90 PRINT "UNDERSTAND WHAT YOU ARE DOING SO READ THE INSTRUCTIONS"
100 PRINT "CAREFULLY."
110 PRINT "AT THE START OF THE GAME, YOU BET ON WINNING. TYPE IN ANY"
120 PRINT "NUMBER BETWEEN 0 AND 200.  I THEN PICK A RANDOM NUMBER"
130 PRINT "YOU ARE TO REACH BY THE SUM TOTAL OF MORE CARDS CHOSEN."
140 PRINT "BECAUSE OF THE RARE CHANCE OF YOU GETTING TO THAT NUMBER"
150 PRINT "EXACTLY, YOU ARE GIVEN AN ALLOWANCE CARD.  THE OBJECT OF"
160 PRINT "THE GAME OF TO GET THE TOTAL OF CARDS WITHIN THE MYSTERY"
170 PRINT "NUMBER WITHOUT GOING OVER."
180 PRINT "YOU ARE GIVEN A HINT AS TO WHAT THE NUMBER IS.  THIS IS NOT"
190 PRINT "THE EXACT NUMBER ONLY ONE CLOSE. ALL YOU DO IN THIS GAME IS"
200 PRINT "DECIDE WHEN TO STOP.  AT THIS POINT YOUR TOTAL IS COMPARED"
210 PRINT "WITH THE NUMBER AND YOUR WINNINGS ARE DETERMINED."
220 M=200
223 D=0
225 T=0
227 O=INT(10*RND(1))+25
229 N=INT(O*RND(1))+O
230 R=(INT(15*RND(1))+1)/100
250 S=INT(2*RND(1)+1)
260 IF S <> 1 THEN 270
262 E=INT(N-(N*R))
265 GOTO 280
270 E=INT(N+(N*R))
280 A=INT(9*RND(11)+2)
283 PRINT
285 PRINT "PLACE YOUR BET ... YOU HAVE $";M;" TO SPEND.";
287 INPUT B
288 PRINT
289 IF B < 0 THEN 297
290 IF M >= B THEN 300
293 PRINT "YOU CAN'T BET MORE THAT YOU'VE GOT!"
295 GOTO 285
297 PRINT "YOU MAY NOT BET AGAINST YOURSELF."
298 GOTO 285
300 PRINT "YOUR 'LUCKY LIMIT' CARD IS A ";A
310 PRINT "YOU MUST COME WITHIN ";A;" WITHOUT GOING OVER TO WIN."
315 PRINT
320 PRINT "HERE WE GO"
322 PRINT
324 PRINT
340 D=D+1
350 C=INT(9*RND(1)+2)
360 PRINT "CARD #";D;" IS A ";C;".YOU ARE TRYING TO COME NEAR ";E
365 T=T+C
370 IF T <= N THEN 380
375 PRINT "YOUR TOTAL IS OVER THE NUMBER";N;" AN AUTOMATIC LOSS!"
377 GOTO 570
380 PRINT "YOUR TOTAL IS ";T;"  DO YOU WANT TO CONTINUE";
385 INPUT Q$
387 PRINT
390 IF LEFT$(Q$,1)="Y" THEN 322
410 IF T < N-A OR T > N THEN 550
500 PRINT "YOU WIN!  THE NUMBER WAS ";N;" YOUR GUESS TOTAL WAS";T
510 PRINT "WITHIN YOUR LIMIT CARD."
520 M=M+B
540 GOTO 600
550 PRINT "YOU BLEW IT!  THE NUMBER WAS ";N;", OUTSIDE YOUR LIMIT BY ";
560 PRINT (N-A)-T
565 PRINT
570 M=M-B
600 PRINT "YOU NOW HAVE $";M;" IN CASH TO BET IN THE NEXT GAME!"
605 IF M <= 0 THEN 655
610 PRINT "WOULD YOU LIKE TO PLAY THE NEXT GAME";
615 INPUT Q$
620 IF LEFT$(Q$,1)="Y" THEN 223
630 PRINT "HOPE YOU HAD FUN."
640 GOTO 999
650 PRINT
655 PRINT CHR$(7);
660 PRINT "YOU ARE BROKE!! YOU MAY NOT PLAY ANYMORE!!"
999 END
